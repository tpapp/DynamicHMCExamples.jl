# # Estimate Bernoulli draws probabilility

# We estimate a simple model of ``n`` independent Bernoulli draws, with
# probability ``Œ±``. First, we load the packages we use.

using TransformVariables
using LogDensityProblems
using DynamicHMC
using MCMCDiagnostics
using Parameters
using Statistics

# Then define a structure to hold the data.
# For this model, the number of draws equal to `1` is a sufficient statistic.

"""
Toy problem using a Bernoulli distribution.

We model `n` independent draws from a ``Bernoulli(Œ±)`` distribution.
"""
struct BernoulliProblem
    "Total number of draws in the data."
    n::Int
    "Number of draws `==1` in the data"
    s::Int
end

# Then make the type callable with the parameters *as a single argument*.  We
# use decomposition in the arguments, but it could be done inside the function,
# too.

function (problem::BernoulliProblem)((Œ±, )::NamedTuple{(:Œ±, )})
    @unpack n, s = problem        # extract the data
    s * log(Œ±) + (n-s) * log(1-Œ±) # log likelihood
end

# We should test this, also, this would be a good place to benchmark and
# optimize more complicated problems.

p = BernoulliProblem(20, 10)
p((Œ± = 0.5, ))

# Recall that we need to
#
# 1. transform from ``‚Ñù`` to the valid parameter domain `(0,1)` for more efficient sampling, and
#
# 2. calculate the derivatives for this transformed mapping.
#
# The helper packages `TransformVariables` and `LogDensityProblems` take care of
# this. We use a flat prior (the default, omitted)

P = TransformedLogDensity(as((Œ± = asùïÄ,)), p)
‚àáP = ForwardDiffLogDensity(P);

# Finally, we sample from the posterior. `chain` holds the chain (positions and
# diagnostic information), while the second returned value is the tuned sampler
# which would allow continuation of sampling.

chain, NUTS_tuned = NUTS_init_tune_mcmc(‚àáP, 1000)

# To get the posterior for ``Œ±``, we need to use `get_position` and
# then transform

posterior = transform.(Ref(‚àáP.transformation), get_position.(chain));

# Extract the parameter.

posterior_Œ± = first.(posterior);

# check the mean

mean(posterior_Œ±)

# check the effective sample size

ess_Œ± = effective_sample_size(posterior_Œ±)

# NUTS-specific statistics

NUTS_statistics(chain)
